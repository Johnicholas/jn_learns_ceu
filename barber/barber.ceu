native _random_plusminus();
native _push();
native _empty();
native _pop();

class Barber with
  event int next;
  var bool idle = true;
  function (void)=>int get_next_id;
do
  var int next_id = 1;
  function (void)=>int get_next_id do
    next_id = next_id + 1;
    return next_id - 1;
  end
end

class Customer with
  var Barber& b;
do
  var int id = b.get_next_id();
  _printf("create %d 10 10\n", id);
  _printf("move %d 400 10\n", id);
  await 2s;
  _printf("place %d 400 10\n", id);
  // seize
  if b.idle then
    b.idle = false;
  else
    _printf("move %d 780 150\n", id);
    _push(id);
    var int next = await b.next until next == id;
    _printf("place %d 780 150\n", id);
  end;
  _printf("move %d 400 300\n", id);
  await (_random_plusminus(15, 3))s; // haircut takes 15+-3 minutes
  _printf("move %d 10 580\n", id);
  // release
  if _empty() then
    b.idle = true;
  else
    emit b.next => _pop();
  end
  await 500ms;
  _printf("destroy %d\n", id);
end

par/or do
  await 480s;
with
  var Barber joe;
  loop do
    await (_random_plusminus(18, 6))s; // new arrivals every 18 +- 6 minutes
    spawn Customer with
      this.b = joe;
    end;
  end
end

escape 0;
