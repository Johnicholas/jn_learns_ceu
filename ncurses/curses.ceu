// based on examples from
// "Introduction to the Unix Curses"
// by Norman Matloff
native do
  ##include <curses.h>
end
native _move();
native _delch();
native _insch();
native _refresh();
native _initscr();
native _cbreak();
native _noecho();
native @nohold _getmaxyx();
native _clear();
native _endwin();
input int GETCH;

var int r;
var int c;
var int nrows;
var int ncols;

function (char dc)=>void draw
do
  _move(r, c);
  _delch();
  _insch(dc);
  _refresh();
  r = r + 1;
  if r == nrows then
    r = 0;
    c = c + 1;
    if c == ncols then
      c = 0;
    end
  end
end

var _WINDOW* wnd;
finalize
  wnd = _initscr();
with
  _endwin();
end

_cbreak();
_noecho();
_getmaxyx(wnd, nrows, ncols);
_clear();
_refresh();

r = 0;
c = 0;
loop do
  var int d = await GETCH;
  if d == 'q' then
    break;
  end
  draw(d);
end
escape 0;
