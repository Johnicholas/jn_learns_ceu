#include "barber.ceu"

var _SDL_Window* win;
finalize
  win = _SDL_CreateWindow("Animator", 100, 100, 800, 600, _SDL_WINDOW_SHOWN);
with
  _SDL_DestroyWindow(win);
end
var int ren_w;
var int ren_h;
_SDL_GetWindowSize(win, &ren_w, &ren_h);

var _SDL_Renderer[] ren;
finalize
  ren = _SDL_CreateRenderer(win, -1, 0);
with
  _SDL_DestroyRenderer(ren);
end

par/or do
  await SDL_QUIT;
with
  // await 480s;
  await FOREVER;
with
  var Queue barber_queue;
  var Queue chair_queue;
  var SmallSquare red with
    this.r = 255;
    this.g = 0;
    this.b = 0;
    this.speed = 0.1;
  end;
  var SmallSquare green with
    this.r = 0;
    this.g = 255;
    this.b = 0;
    this.speed = 0.1;
  end;
  var SmallSquare blue with
    this.r = 0;
    this.g = 0;
    this.b = 255;
    this.speed = 0.1;
  end;
  pool Barber[] barbers;
  pool Chair[] chairs;
  par do
    loop i in 10 do
      spawn Barber in barbers with
        this.customers = barber_queue;
        this.kind = red;
        this.ren = ren;
        this.id = i;
      end;
      await (_random_plusminus(18000+2000*i, 1000))ms; // new arrivals every 18 +- 6 minutes
    end
  with
    loop i in 10 do
      await (_random_plusminus(18000+2000*i, 1000))ms; // new arrivals every 18 +- 6 minutes
      spawn Chair in chairs with
        this.customers = chair_queue;
        this.kind = green;
        this.ren = ren;
        this.id = i;
      end;
    end
  with
    // every once in a while, a customer enters the shop
    loop i do
      await (_random_plusminus(2000, 1000))ms; // new arrivals every 18 +- 6 minutes
      spawn Customer with
        this.barbers = barber_queue;
        this.chairs = chair_queue;
        this.kind = blue;
        this.ren = ren;
        this.id = i;
      end;
    end
  end
end

escape 0;

